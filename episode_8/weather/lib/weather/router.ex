defmodule Weather.Router do
  use Plug.Router

  plug Plug.Parsers, parsers: [:json],
                     pass: ["text/*"],
                     json_decoder: Jason

  plug :match
  plug :dispatch

  get "/" do
    render(conn, "index.html")
  end

  post "/fetch" do
    %{"city" => city} = conn.body_params
    data = Weather.fetch(city)
    render_json(conn, data)
  end

  get "/ping" do
    send_resp(conn, 200, "pong")
  end

  match _ do
    send_resp(conn, 404, "404!")
  end

  defp render(%{status: status} = conn, template, assigns \\ []) do
    body =
      "lib/weather/templates"
      |> Path.join(template)
      |> String.replace_suffix(".html", ".html.eex")
      |> EEx.eval_file(assigns)

    send_resp(conn, (status || 200), body)
  end

  defp render_json(%{status: status} = conn, data) do
    body = Jason.encode!(data)
    send_resp(conn, (status || 200), body)
  end
end
